# ─────────────────────────────────────────────────────────────────────────────
# BOOTSTRAP — Run this ONCE before anything else.
# Creates the S3 bucket and DynamoDB table that store Terraform state.
#
# Usage:
#   cd iac/bootstrap
#   terraform init
#   terraform apply
# ─────────────────────────────────────────────────────────────────────────────

provider "aws" {
  region = "ap-northeast-1"

  default_tags {
    tags = {
      Owner       = "quyet_nc"
      Email       = "quyetnc99@gmail.com"
      Environment = "bootstrap"
      ManagedBy   = "Terraform"
    }
  }
}

locals {
  common_tags = {
    Owner       = "quyet_nc"
    Email       = "quyetnc99@gmail.com"
    Environment = "bootstrap"
    ManagedBy   = "Terraform"
  }
}

resource "aws_s3_bucket" "tfstate" {
  bucket = "proops2026-tfstate-quyetnc"

  # lifecycle {
  #   prevent_destroy = true
  # }
  tags = merge(local.common_tags, { Name = "proops2026-tfstate-quyetnc" })
}

resource "aws_s3_bucket_versioning" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id
  versioning_configuration { status = "Enabled" }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id
  rule {
    apply_server_side_encryption_by_default { sse_algorithm = "AES256" }
  }
}

resource "aws_s3_bucket_public_access_block" "tfstate" {
  bucket                  = aws_s3_bucket.tfstate.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_dynamodb_table" "tfstate_lock" {
  name         = "proops2026-tfstate-lock-quyetnc"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }

  tags = merge(local.common_tags, { Name = "proops2026-tfstate-lock-quyetnc" })
}

output "s3_bucket" {
  value = aws_s3_bucket.tfstate.bucket
}

output "dynamodb_table" {
  value = aws_dynamodb_table.tfstate_lock.name
}
